「..\プロデルテストユニット.rdr」を参照する

マニュアル基本編テストとは
	テストケースを継承する
	
テストする手順
※テスト
	窓というウィンドウを作る
	窓へOKボタンというボタンを作る
	OKボタンの位置を｛30,30｝に変える
	OKボタンの内容を「ボタン」に変える
終わり

代入テストする手順
※テスト
	【結果】
	結果を10とする
	「A」として、結果が10と等しいはず

	結果=120
	「B」として、結果が120と等しいはず

	メッセージへ「こんにちは」を代入する
	「C」として、メッセージが「こんにちは」と等しいはず

	「D」として、　１　＋　１　が2と等しいはず

	値段は、150
	結果は、値段×1.05
	「E」として、結果が157.5と等しいはず
	
	結果　は、　　（５＋２）×３＋（１８＋６）÷４＋５０　
	「F」として、結果が77と等しいはず
終わり

繰り返しテストする手順
※テスト
	【結果】
	結果は、「」
	3回、繰り返す
		結果は、結果＆「わん」
	繰り返し終わり
	結果が「わんわんわん」と等しいはず

	結果は、「」
	3回、『結果は、結果＆「わん」』を繰り返す
	結果が「わんわんわん」と等しいはず

	結果は、「」
	値は、1
	3回、値にカウントしながら繰り返す
	　　結果は、結果＆「[値]回目です。」
	繰り返し終わり
	結果が「1回目です。2回目です。3回目です。」と等しいはず

	結果は、「」
	値を10から2ずつ増やしながら20まで繰り返す
		結果 = 結果 + 1 
	繰り返し終わり
	値が22と等しいはず
	結果が6と等しいはず

	値は、１
	（値が５以下）の間、繰り返す
		値は、値＋１
	繰り返し終わり
	値が6と等しいはず

	値は、０
	（値が５）になるまで繰り返す
		値は、値＋１
	繰り返し終わり
	値が5と等しいはず

	一覧は、｛「N-03B」，「F-04B」，「P-09A」，「SH-08B」｝
	結果は、「」
	一覧を要素へそれぞれ繰り返す
		結果は、結果＆要素
	繰り返し終わり
	結果が「N-03BF-04BP-09ASH-08B」と等しいはず

	名前一覧は、｛「裕太」，「健司」，「智美」，「直子」｝
	結果は、「」
	名前一覧のすべての名前についてそれぞれ繰り返す
		結果は、結果＆名前
	繰り返し終わり
	結果が「裕太健司智美直子」と等しいはず

	値は、1
	3回、変数にカウントしながら繰り返す
		繰り返しを抜ける
	繰り返し終わり
	値が1と等しいはず

	値は、0
	3回、繰り返す
		値は、値＋1
		繰り返しを続ける
		//この次からは実行されません
		失敗させる
	繰り返し終わり
	値が3と等しいはず
終わり

判断式テストする手順
※テスト
	メッセージは、「こんにちは」
	もしメッセージが「こんにちは」なら
	そうでなければ
		「文字列比較」として失敗させる
	もし終わり

	数字は、3
	もし数字が１なら
		「数値比較1」として失敗させる
	他でもし数字が２なら
		「数値比較2」として失敗させる
	そうでないなら
	もし終わり

	【正解】は、１２３

	もし123が正解なら
	そうでなければ
		「数値比較3」として失敗させる
	もし終わり
	もし130が正解より大きいなら
	そうでなければ
		「数値大小比較1」として失敗させる
	もし終わり
	もし100が正解より小さいなら
	そうでなければ
		「数値大小比較2」として失敗させる
	もし終わり

	もし123＝正解なら
	そうでなければ
		「数値比較記号1」として失敗させる
	もし終わり
	もし130≧正解なら
	そうでなければ
		「数値比較記号2」として失敗させる
	もし終わり
	もし100≦正解なら
	そうでなければ
		「数値比較記号3」として失敗させる
	もし終わり

	数字１は　１
	数字２は　２
	もし数字１が１かつ数字２が１なら
		「論理演算1」として失敗させる
	そうでなければ
	もし終わり
	もし数字１が１または数字２が3なら
	そうでなければ
		「論理演算2」として失敗させる
	もし終わり

	果物は、｛「りんご」、「バナナ」、「ぶどう」、「なし」｝
	「配列要素取得」として果物（２）が「バナナ」と等しいはず

	果物（１）は、「りんご」
	果物（２）は、「バナナ」
	果物（３）は、「ぶどう」
	果物（４）は、「なし」
	「配列内容取得」として、果物が「{りんご,バナナ,ぶどう,なし}」と等しいはず

	やさいは、「にんじん、トマト、きゅうり」を「、」で区切ったもの
	「区切ったもの」として、やさい（２）が「トマト」と等しいはず

	【値段表】
	値段表（「りんご」）は、「120円」
	値段表（「バナナ」）は、「80円」
	値段表（「ぶどう」）は、「300円」
	「辞書要素1」として、値段表（「りんご」）が「120円」と等しいはず

	車の「色」は、「白」
	「辞書要素2」として、車の「色」が「白」と等しいはず

	値段表（「りんご」）は、「120円」
	値段表（「バナナ」）は、「80円」
	値段表（「ぶどう」）は、「300円」
	「見出し」として、値段表の見出しが「{りんご,バナナ,ぶどう}」と等しいはず

	値段表は、｛りんご＝「120円」、バナナ＝「80円」、ぶどう＝「300円」｝
	「辞書定数」として、値段表（「バナナ」）が「80円」と等しいはず
	終わり
終わり